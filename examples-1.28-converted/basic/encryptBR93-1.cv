(* encryption scheme by Bellare and Rogaway, Random Oracles are
Practical: a Paradigm for Designing Efficient Protocols, CCS'93, 
section 3.1: E(x) = f(r) || H(r) xor x (CPA) *)

(* In order to determine the proof, use interactive mode: 
   set interactiveMode = true.
The proof is as follows: *)

proof {
crypto rom(hash);
remove_assign binder pk;
show_game;
crypto ow(f) r;
crypto remove_xor(xor) *;
simplify;
success
}

param nx.

type key [bounded].
type keyseed [large,fixed].
type hasht [large,fixed].
type seed [large,fixed]. 

(* One-way trapdoor permutation *)

proba POW.

expand OW_trapdoor_perm(keyseed, key, key, seed, pkgen, skgen, f, mf, POW).

(* Hash function, random oracle model *)

type hashkey [fixed].

expand ROM_hash(hashkey, seed, hasht, hash).

param qH [noninteractive].
channel hc1, hc2.


(* Xor *)

expand Xor(hasht, xor, zero).

(* Queries *)

query secret b1 [onesession].

channel c1, c2, c3, c4, c5, hstart, hret, start.



process 
	in(hstart, ());
new hk: hashkey;
out(hret, ());
((
  ! i_1 <= qH
  in(hc1[i_1], x_4: seed);
  out(hc2[i_1], hash(hk, x_4))
) | (
  in(start, ());
  new r: keyseed;
  let sk: key = skgen(r) in
  let pk: key = pkgen(r) in
  out(c5, pk);
  in(c3, (m1: hasht, m2: hasht));
  new b1: bool;
  let menc: hasht = (if b1 then m1 else m2) in
  new x_5: seed;
  let a_1: seed = f(pk, x_5) in
  let b: hasht = xor(hash(hk, x_5), menc) in
  out(c4, (a_1, b))
))




(* EXPECTED
All queries proved.
0.040s (user 0.024s + system 0.016s), max rss 38448K
END *)
