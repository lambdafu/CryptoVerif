(* 1 round of Public-key Kerberos protocol. fixed. *)

(*
The desired query
query w:bitstring, x:maxmac, y:maxmac, z:maxmac, N:nonce;
       event fullCK(K,T,N,w,x,y) ==> fullKC(C,T,N,w,z,y).		 
is not proved.

In CryptoVerif 1.09, it is proved.
In CryptoVerif 1.10, one can prove it by adding

     ;
SArename z_270;
success

at the end of the proof.

In CryptoVerif 1.20, it is proved again.

Two points:

1) in the game

Game 46 is
in(start, ());
new rkC: keyseed;
let pkC: pkey = pkgen2(rkC) in
new rkCs: skeyseed;
let spkC: spkey = spkgen2(rkCs) in
new rkKs: skeyseed;
let spkK: spkey = spkgen2(rkKs) in
new rkCA: skeyseed;
let pkCA: spkey = spkgen2(rkCA) in
new rKt: symkeyseed;
let Kt: key = kgen(rKt) in
let x_53: sblocksize = concat1(C, pkC, spkC) in
new q1: sseed;
let certC: signature = sign2(x_53, sskgen2(rkCA), q1) in
out(c25, (pkC, spkC, spkK, pkCA));
(
  (
    (
      ! !_31 <= N
      in(c1[!_31], hostT: tgs);
      new n2: nonce;
      new tc0: timest;
      let x_76: sblocksize = padno(tc0, n2) in
      new r1: sseed;
      new n1: nonce;
      out(c2[!_31], (C, pkC, spkC, certC, tc0, n2, sign2(x_76, sskgen2(rkCs), r1), C, hostT, n1));
      in(c3[!_31], (m21: bitstring, =C, TGT_38: maxmac, m24: maxmac));
      find @i_133 <= N suchthat defined(tk_96[@i_133], AK_35[@i_133], z_230[@i_133], n5[@i_133], hostW[@i_133], ms4[@i_133], tc1[@i_133], ms3[@i_133], m2_118[@i_133]) && (m21 = m2_118[@i_133]) then
        let hostZ: kas = K in
        let x_74: sblocksize = padno(tc0, n2) in
        find  suchthat defined(tk_96[@i_133], AK_35[@i_133], z_230[@i_133], n5[@i_133], hostW[@i_133], ms4[@i_133], tc1[@i_133], ms3[@i_133]) && ((certC = ms3[@i_133]) && ((tc0 = tc1[@i_133]) && ((!_31 = @i_78[@i_133]) && ((sign2(x_74, sskgen2(rkCs), r1) = ms4[@i_133]) && ((hostT = hostW[@i_133]) && (n1 = n5[@i_133])))))) then
          if defined(tk_96[@i_133], AK_35[@i_133], z_230[@i_133]) && (z_230[@i_133] = m24) then
          event fullCK(hostZ, hostT, n1, m21, TGT_38, m24);
          out(c18[!_31], acceptC1(hostZ, hostT))
	[[ 
	CryptoVerif might realize that the next branch of find is impossible:
	it requires defining z_201[@i_133] and we already know that m2_118[@i_133] is defined,
	but these two variables are defined in different branches of find below, so
	they cannot be defined simultaneously. CryptoVerif tests that (check_compatible_main in src/simplify.ml)
	but only for variables with the current replication indices.
	]]
        orfind  suchthat defined(z_201[@i_133], n5[@i_133], hostW[@i_133], ms4[@i_133], tc1[@i_133], ms3[@i_133]) && ((certC = ms3[@i_133]) && ((tc0 = tc1[@i_133]) && ((!_31 = @i_78[@i_133]) && ((sign2(x_74, sskgen2(rkCs), r1) = ms4[@i_133]) && ((hostT = hostW[@i_133]) && (n1 = n5[@i_133])))))) then
          if defined(z_201[@i_133]) && (z_201[@i_133] = m24) then
          event fullCK(hostZ, hostT, n1, m21, TGT_38, m24);
          out(c18[!_31], acceptC1(hostZ, hostT))
      orfind @i_132 <= N suchthat defined(z_201[@i_132], AK_35[@i_132], n5[@i_132], hostW[@i_132], ms4[@i_132], tc1[@i_132], ms3[@i_132], m2_123[@i_132]) && (m21 = m2_123[@i_132]) then
        let hostZ: kas = K in
        let x_74: sblocksize = padno(tc0, n2) in
        find  suchthat defined(tk_96[@i_132], AK_35[@i_132], z_230[@i_132], n5[@i_132], hostW[@i_132], ms4[@i_132], tc1[@i_132], ms3[@i_132]) && ((certC = ms3[@i_132]) && ((tc0 = tc1[@i_132]) && ((!_31 = @i_78[@i_132]) && ((sign2(x_74, sskgen2(rkCs), r1) = ms4[@i_132]) && ((hostT = hostW[@i_132]) && (n1 = n5[@i_132])))))) then
          if defined(tk_96[@i_132], AK_35[@i_132], z_230[@i_132]) && (z_230[@i_132] = m24) then
          event fullCK(hostZ, hostT, n1, m21, TGT_38, m24);
          out(c18[!_31], acceptC1(hostZ, hostT))
        orfind  suchthat defined(z_201[@i_132], n5[@i_132], hostW[@i_132], ms4[@i_132], tc1[@i_132], ms3[@i_132]) && ((certC = ms3[@i_132]) && ((tc0 = tc1[@i_132]) && ((!_31 = @i_78[@i_132]) && ((sign2(x_74, sskgen2(rkCs), r1) = ms4[@i_132]) && ((hostT = hostW[@i_132]) && (n1 = n5[@i_132])))))) then
          if defined(z_201[@i_132]) && (z_201[@i_132] = m24) then
          event fullCK(hostZ, hostT, n1, m21, TGT_38, m24);
          out(c18[!_31], acceptC1(hostZ, hostT))
    |
      ! !_32 <= N
      in(c22[!_32], (hostY: client, pkY: pkey, spkY: spkey, ms3: signature, tc1: timest, n4: nonce, ms4: signature, hostV: client, hostW: tgs, n5: nonce));
      if (hostV = hostY) then
      if (((hostY = C) && ((pkY = pkC) && (spkY = spkC))) && check2(concat1(hostY, pkY, spkY), pkCA, ms3)) then
      find @i_78 <= N suchthat defined(n2[@i_78], tc0[@i_78], x_74[@i_78]) && (((tc1 = tc0[@i_78]) && (n4 = n2[@i_78])) && check2(padno(tc1, n4), spkY, ms4)) then
        find j1 <= N2 suchthat defined(Qkey[j1], Lhost[j1]) && (Lhost[j1] = hostW) then
        new s3_92: symkeyseed;
        let AK_35: key = kgen(s3_92) in
        new tk_96: timest;
        new r4_95: symseed;
        let TGT_37: maxmac = encsym(concat5(AK_35, tk_96, hostY), Qkey[j1], r4_95) in
        let x_231: maxenc = concat4(AK_35, n5, tk_96, hostW) in
        new r5_97: symseed;
        new s1_93: symkeyseed;
        let z_230: maxmac = encsym(x_231, kgen2(s1_93), r5_97) in
        new r7_99: seed;
        let m2_118: bitstring = enc2(Z, pkY, r7_99) in
        let m5: bitstring = (m2_118, hostY, TGT_37, z_230) in
        event fullKC(hostY, hostW, n5, m2_118, TGT_37, z_230);
        out(c23[!_32], m5)
      orfind @i_78 <= N suchthat defined(n2[@i_78], tc0[@i_78], x_76[@i_78]) && (((tc1 = tc0[@i_78]) && (n4 = n2[@i_78])) && check2(padno(tc1, n4), spkY, ms4)) then
        find j1 <= N2 suchthat defined(Qkey[j1], Lhost[j1]) && (Lhost[j1] = hostW) then
        new s3_100: symkeyseed;
        let AK_35: key = kgen(s3_100) in
        new r4_103: symseed;
        new tk_104: timest;
        let TGT_37: maxmac = encsym(concat5(AK_35, tk_104, hostY), Qkey[j1], r4_103) in
        let x_202: maxenc = concat4(AK_35, n5, tk_104, hostW) in
        new s1_101: symkeyseed;
        new r5_105: symseed;
        let z_201: maxmac = encsym(x_202, kgen2(s1_101), r5_105) in
        new r7_107: seed;
        let m2_123: bitstring = enc2(Z, pkY, r7_107) in
        let m5: bitstring = (m2_123, hostY, TGT_37, z_201) in
        event fullKC(hostY, hostW, n5, m2_123, TGT_37, z_201);
        out(c23[!_32], m5)
    )
  |
    ! !_33 <= N2
    in(c21[!_33], (Lhost: tgs, Lkey: key));
    if (Lhost = T) then
      let Qkey: key = Kt
    else
      let Qkey: key = Lkey
  )
|
  ! !_34 <= N2
  in(c16[!_34], (Mhost1: tgs, Mhost2: server, Mkey: key))
)


2) The equation
m2_118[@i_133[!_253]] = m2_123[@i_133[!_253]]
obtained when trying to prove the query 
query w:bitstring, x:maxmac, y:maxmac, z:maxmac, N:nonce;
       event fullCK(K,T,N,w,x,y) ==> fullKC(C,T,N,w,z,y).		 
should be oriented. Then I think the query would be proved.

The reason why it is not oriented is that we have
(m2_118[@i_133[!_253]] >>= enc2(Z, pkY[@i_133[!_253]], r7_99[@i_133[!_253]]))
(m2_123[@i_133[!_253]] >>= enc2(Z, pkY[@i_133[!_253]], r7_107[@i_133[!_253]]))
so after applying these substitutions, we obtain an equality between two
enc2(...) which we do not know how to orient.

Similarly, the equation
(z_230[@i_133[!_253]] = z_201[@i_133[!_253]])
is not oriented.

3) We obtain the facts
fullKC(hostY[@i_133[!_253]], hostW[@i_133[!_253]], n5[@i_133[!_253]], m2_118[@i_133[!_253]], TGT_37[@i_133[!_253]], z_230[@i_133[!_253]])
and
fullKC(hostY[@i_133[!_253]], hostW[@i_133[!_253]], n5[@i_133[!_253]], m2_123[@i_133[!_253]], TGT_37[@i_133[!_253]], z_201[@i_133[!_253]])
while we would need
fullKC(hostY[@i_133[!_253]], hostW[@i_133[!_253]], n5[@i_133[!_253]], m2_118[@i_133[!_253]], TGT_37[@i_133[!_253]], z_201[@i_133[!_253]])
									^^^^					     ^^^^
and cannot proved the equality between them because the equations are not oriented.


*)


(* The proof for PKINIT was *)
proof {
crypto sign rkCA;
crypto sign rkCs;
crypto enc rkC;
crypto sign rkKs;
success;
auto
}

param N.
param N2.
param N3.
param N4.

type nonce [fixed,large].
type client [bounded].
type kas [bounded].
type tgs [bounded].
type server [bounded].

(* types for public-key crypto: *)
type pkey [bounded].
type skey [bounded].
type keyseed [large,fixed].
type seed [fixed].
type spkey [bounded].
type sskey [bounded].
type skeyseed [large,fixed].
type sseed [fixed].  
type signature [bounded].
type blocksize [fixed].
type blocksizebot [bounded].
type sblocksize [bounded].

(* types for symmetric enc: *)
type symkeyseed [fixed].
type key [fixed].
type protkey [fixed].
type usenum1 [fixed].
type usenum2 [fixed].
type macs [fixed].
type mkeyseed [fixed].
type mkey [fixed].
type symseed [fixed].
type maxmac [bounded].
type maxenc [bounded].
type timest [fixed].

(* type for checksum: *)
type checksum [fixed].


(* concatenation functions, needed to be able to apply, e.g., encryption to a list: *)
(*public-key:*)
fun concat1(client, pkey, spkey):sblocksize [data].							(* for client  certificates *)
fun concat3(key, mkey, (*checksum*) macs):sblocksize [data]. 	     	       	  	    		(* for signature over {k1, k2, ck}*)
fun concat7(kas, pkey, spkey):sblocksize [data].    							(* for kas certificate *)
fun padno(timest, nonce):sblocksize [data]. 	    	   	      	      	   	   	     	(* to sign tc0, n2*)
fun concat2(kas, pkey, spkey, signature, key, mkey, macs, signature):blocksize [data]. 			(* for encryption {certK, {k,ck}_skK } *)
fun concat4(key, nonce, timest, tgs):maxenc [data]. 						   	(* for authenticator {AK, n1, t_K, T}_k *)
fun concat5(key, timest, client):maxenc [data]. 							(* for TGT and ST *)
fun concat8(key, nonce, timest, server):maxenc [data]. 		       				(* for {SK, n2, t_T, S}_AK in TG_reply *)
fun pad(client, timest):maxenc [data].								(* for {C,t}_SK *)
fun padts(timest):maxenc [data].									(* for {t}_SK *)
fun concat6(client, pkey, spkey, signature, timest, nonce, signature, client, tgs, nonce):maxmac [data].  	(* for checksum of first message *)


(* inequalities of outputs of two different concat functions when they output the same types : *) 

equation forall y:key, x:timest, z:client, t2:key, y2:nonce, x2:timest, z2:tgs;        	  concat5(y,x,z) <> concat4(t2,y2,x2,z2).
equation forall t:key, y: nonce, x:timest, z:server, t2:key, y2:nonce, x2:timest, z2:tgs;  concat8(t,y,x,z) <> concat4(t2,y2,x2,z2).
equation forall z:client, t:timest, t2:key, y2:nonce, x2:timest, z2:tgs;  	 	  pad(z,t) <> concat4(t2,y2,x2,z2).
equation forall t: timest, t2:key, y2:nonce, x2:timest, z2:tgs;  			  padts(t) <> concat4(t2,y2,x2,z2).
equation forall y:key, x:timest, z:client, t2: key, y2:nonce, x2:timest, z2:server;   	  concat5(y,x,z) <> concat8(t2,y2,x2,z2).
equation forall y:key, x:timest, z:client, t2: timest, z2:client;    			  concat5(y,x,z) <> pad(z2,t2). 
equation forall y:key, x:timest, z:client, t2: timest;    				  concat5(y,x,z) <> padts(t2). 
equation forall t:key, y:nonce, x:timest, z:server, t2:timest, z2:client;  		  concat8(t,y,x,z) <> pad(z2,t2).
equation forall t:key, y:nonce, x:timest, z:server, t2:timest;  				  concat8(t,y,x,z) <> padts(t2).
equation forall t: timest, z: client, t2: timest;   					  pad(z,t)<> padts(t2).

equation forall k1:key, k2:mkey, y:(*checksum*) macs, z2:client, y2:pkey, x2:spkey; 		  concat3(k1,k2,y) <> concat1(z2,y2,x2).
equation forall k1:key, k2:mkey, y:(*checksum*) macs, t2:timest, z2: nonce; 			  concat3(k1,k2,y) <> padno(t2,z2).
equation forall t:timest, y:nonce, z2:client, y2:pkey, x2:spkey;				  padno(t,y) <> concat1(z2,y2,x2).
equation forall k1:key, k2:mkey, y:(*checksum*)macs, z2:kas, y2:pkey, x2:spkey;		  concat3(k1,k2,y) <> concat7(z2,y2,x2).
equation forall t:timest, y:nonce, z2:kas, y2:pkey, x2:spkey; 	     			  padno(t,y) <> concat7(z2,y2,x2).
equation forall z2:client, y2:pkey, x2:spkey, z:kas, y:pkey, x:spkey;		  	  concat1(z2,y2,x2) <> concat7(z,y,x).


(* Public-key encryption (CCA2): *)

proba Penc.
proba Penccoll.

fun enc(blocksize, pkey, seed):bitstring.
fun skgen(keyseed):skey.
fun pkgen(keyseed):pkey.
fun dec(bitstring, skey):blocksizebot.

fun enc2(blocksize, pkey, seed):bitstring.
fun skgen2(keyseed):skey.
fun pkgen2(keyseed):pkey.
fun dec2(bitstring, skey):blocksizebot.

fun injbot1(blocksize):blocksizebot [data].

const Z:blocksize.

equation forall m:blocksize, r:keyseed, r2:seed; 
	dec(enc(m, pkgen(r), r2), skgen(r)) = injbot1(m).
equation forall m:blocksize, r:keyseed, r2:seed; 
	dec2(enc2(m, pkgen2(r), r2), skgen2(r)) = injbot1(m).

equiv
  ! N3 new r: keyseed; (
    gen_Oracle() := return(pkgen(r)) |
    ! N2 gen_Oracle_1(m: bitstring) := return(dec(m, skgen(r))))|
  ! N4 new r2: seed; gen_Oracle_2(x: blocksize, y: pkey) := return(enc(x,y,r2)) [all]
<=(N3 * N4 * Penc(time, N2))=>
  ! N3 new r: keyseed; (
    gen_Oracle() := return(pkgen2(r)) |
    ! N2 gen_Oracle_1(m: bitstring) := find j <= N4 suchthat defined(m2[j], y[j], x[j]) && y[j] = pkgen2(r) && m = m2[j] then return(injbot1(x[j])) else return(dec2(m, skgen2(r))))|
  ! N4 gen_Oracle_2(x: blocksize, y: pkey) := find k <= N3 suchthat defined(r[k]) && (y = pkgen2(r[k])) then (new r2: seed; let m2:bitstring = enc2(Z, y, r2) in return(m2)) else new r3: seed; return(enc(x,y,r3)).

collision new r1: keyseed; new r2: keyseed; return(pkgen(r1) = pkgen(r2)) <=(Penccoll)=> return(r1 = r2).
collision new r1: keyseed; new r2: keyseed; return(pkgen2(r1) = pkgen2(r2)) <=(Penccoll)=> return(r1 = r2).
collision new r1: keyseed; new r2: keyseed; return(skgen(r1) = skgen(r2)) <=(Penccoll)=> return(r1 = r2).
collision new r1: keyseed; new r2: keyseed; return(skgen2(r1) = skgen2(r2)) <=(Penccoll)=> return(r1 = r2).


(* Signatures: *)

proba Psign.
proba Psigncoll.

fun sign(sblocksize, sskey, sseed):signature.
fun sskgen(skeyseed):sskey.
fun spkgen(skeyseed):spkey.
fun check(sblocksize, spkey, signature):bool.

fun sign2(sblocksize, sskey, sseed):signature.
fun sskgen2(skeyseed):sskey.
fun spkgen2(skeyseed):spkey.
fun check2(sblocksize, spkey, signature):bool.

equation forall m:sblocksize, r:skeyseed, r2:sseed; 
	check(m, spkgen(r), sign(m, sskgen(r), r2)) = true.
equation forall m:sblocksize, r:skeyseed, r2:sseed; 
	check2(m, spkgen2(r), sign2(m, sskgen2(r), r2)) = true.

equiv
  ! N3 new r: skeyseed; (
    gen_Oracle_1() := return(spkgen(r)) |
    ! N2 new r2: sseed; gen_Oracle_2(x: sblocksize) := return(sign(x, sskgen(r), r2)))|
  ! N4 gen_Oracle_3(m: sblocksize, y: spkey, si: signature) := return(check(m, y, si)) [all]
<=(N3 * Psign(time, N2))=>
  ! N3 new r: skeyseed; (
    gen_Oracle_1() := return(spkgen2(r)) |
    ! N2 new r2: sseed; gen_Oracle_2(x: sblocksize) := return(sign2(x, sskgen2(r), r2)))|
  ! N4 gen_Oracle_3(m: sblocksize, y: spkey, si: signature) := find j <= N2, k <= N3 suchthat defined(x[j, k], r[k]) && y = spkgen2(r[k]) && m = x[j,k] && check2(m, y, si) then return(true) else find k <= N3 suchthat defined(r[k]) && (y = spkgen2(r[k])) then return(false) else return(check(m,y,si)).

collision new r1: skeyseed; new r2: skeyseed; return(spkgen(r1) = spkgen(r2)) <=(Psigncoll)=> return(r1 = r2).
collision new r1: skeyseed; new r2: skeyseed; return(spkgen2(r1) = spkgen2(r2)) <=(Psigncoll)=> return(r1 = r2).
collision new r1: skeyseed; new r2: skeyseed; return(sskgen(r1) = sskgen(r2)) <=(Psigncoll)=> return(r1 = r2).
collision new r1: skeyseed; new r2: skeyseed; return(sskgen2(r1) = sskgen2(r2)) <=(Psigncoll)=> return(r1 = r2). 


(* Shared-key encryption (CPA Stream cipher + INT-CTXT): *)

proba Pencsym.
proba Pencctxt.

fun encsym(maxenc,key,symseed):maxmac.
fun kgen(symkeyseed):key.
fun decsym(maxmac,key):bitstringbot.

fun encsym2(maxenc,key,symseed):maxmac.
fun kgen2(symkeyseed):key.

fun injbot2(maxenc):bitstringbot [data].
equation forall x:maxenc; injbot2(x) <> bottom.

(* The function Z2 returns for each bitstring, a bitstring of the same length, consisting only of zeroes. *)
fun Z2(maxenc):maxenc.

const Zconcat4:maxenc.
const Zconcat5:maxenc.
const Zconcat8:maxenc.
const  	  Zpad:maxenc.
const   Zpadts:maxenc. 

equation forall m:maxenc, r:symkeyseed, r2:symseed; 
	decsym(encsym(m, kgen(r), r2), kgen(r)) = injbot2(m).

equation forall m:key, y:nonce, x:timest, z:tgs; 
	Z2(concat4(m,y,x,z)) = Zconcat4.
equation forall y:key, x:timest, z:client; 
	Z2(concat5(y,x,z)) = Zconcat5.
equation forall t:key, y:nonce, x:timest, z:server;
	Z2(concat8(t, y, x, z)) = Zconcat8.
equation forall z:client, t:timest;
        Z2(pad(z,t)) = Zpad.
equation forall t: timest;
        Z2(padts(t)) = Zpadts.

equiv
  ! N2 new r: symkeyseed; ! N new r2: symseed; gen_Oracle_1(x: maxenc) := return(encsym(x, kgen2(r), r2))
<=(N2 * Penc(time, N))=>
  ! N2 new r: symkeyseed; ! N new r2: symseed; gen_Oracle_1(x: maxenc) := return(encsym2(Z2(x), kgen2(r), r2)).

equiv
  ! N2 new r: symkeyseed; (
    ! N new r2: symseed; gen_Oracle_1(x: maxenc) := return(encsym(x, kgen(r), r2)) |
    ! N3 gen_Oracle_2(y: maxmac) := return(decsym(y,kgen(r))))
<=(N2 * Pencctxt(time, N, N3))=>
  ! N2 new r: symkeyseed; (
    ! N new r2: symseed; gen_Oracle_1(x: maxenc) := let z:maxmac = encsym(x, kgen2(r), r2) in return(z) |
    ! N3 gen_Oracle_2(y: maxmac) := find j <= N suchthat defined(x[j], r2[j], z[j]) && (z[j] = y) then return(injbot2(x[j])) else return(bottom)).


(* Unforgeable Mac: *)

proba Pmac.

fun mac(maxmac, mkey):macs.
fun checkmac(maxmac, mkey, macs): bool.
fun mkgen(mkeyseed):mkey.

fun mac2(maxmac, mkey):macs.
fun checkmac2(maxmac, mkey, macs): bool.
fun mkgen2(mkeyseed):mkey.


equation forall m:maxmac, r: mkeyseed;
	checkmac(m, mkgen(r), mac(m, mkgen(r))).
equation forall m:maxmac, r:mkeyseed;
	checkmac2(m, mkgen2(r), mac2(m, mkgen2(r))).


equiv
  ! N3 new r: mkeyseed; (
    ! N gen_Oracle_1(x: maxmac) := return(mac(x, mkgen(r))) |
    ! N2 gen_Oracle_2(m: maxmac, ma: macs) := return(checkmac(m, mkgen(r), ma)))
<=(N3 * Pmac(time, N, N2))=>
  ! N3 new r: mkeyseed; (
    ! N gen_Oracle_1(x: maxmac) := return(mac2(x, mkgen2(r))) |
    ! N2 gen_Oracle_2(m: maxmac, ma: macs) := find j <= N suchthat defined(x[j]) && (m = x[j]) && (ma = mac2(x[j], mkgen2(r))) then return(true) else return(false)).


(* Queries *)

channel c1, c2, c3, c4, c5, c6, c7, c8, c9, c10, c11, c12, c13, c14, c15, c16, c17, c18, c19, c20, c21, c22, c23, c24, c25,
	 start, finish, cC, cK.

const C :client.
const K :kas.
const T :tgs.
const S :server.

fun acceptC1(kas, tgs):bitstring.
fun acceptC2(tgs, server):bitstring.
fun acceptC3(server):bitstring.
fun acceptK(client):bitstring.
fun acceptT(client, server):bitstring.
fun acceptS(client): bitstring.


event fullKC(client, tgs, nonce, bitstring, maxmac, maxmac).
event fullCK(kas, tgs, nonce, bitstring, maxmac, maxmac).		
event partCT(tgs, maxmac, maxmac).			
event fullCT(tgs, server, nonce, maxmac, maxmac, maxmac).  
event partTC(client, maxmac, maxmac).   		
event fullTC(client, server, nonce, maxmac, maxmac, maxmac, maxmac).

event partCS(server, tgs, maxmac, maxmac).
event fullCS(server, tgs, maxmac, maxmac, maxmac).
event partSC(client, maxmac, maxmac).
event fullSC(client, maxmac, maxmac, maxmac).      		



(* authentication queries : *)
(*
query w:bitstring, x:maxmac, y:maxmac, z:maxmac, N:nonce;
       event inj:fullCK(K,T,N,w,x,y) ==> inj:fullKC(C,T,N,w,z,y).
*)
query z: maxmac, y: maxmac, x: maxmac, w: bitstring, N: nonce;
  event(fullCK(K, T, N, w, x, y)) ==> event(fullKC(C, T, N, w, z, y)).		 
query z: maxmac, w': bitstring, y: maxmac, x: maxmac, w: bitstring, N: nonce;
  event(fullCK(K, T, N, w, x, y)) ==> event(fullKC(C, T, N, w', z, y)).		 
query y': maxmac, z: maxmac, y: maxmac, x: maxmac, w: bitstring, N: nonce;
  event(fullCK(K, T, N, w, x, y)) ==> event(fullKC(C, T, N, w, z, y')).		 

(*
query  w: bitstring,  x:maxmac, x':maxmac, y:maxmac, y': maxmac, N:nonce;				   
        event partTC(C,x,y) ==> partCT(T,x',y) && fullKC(C,T,N,w,x,y').
						  	
query z:maxmac, z':maxmac, y:maxmac, x:maxmac, x':maxmac, v:maxmac, N:nonce;
      	event inj:fullCT(T,S,N,x,z,y) ==> inj: fullTC(C,S,N,x',z',v,y).

query z:maxmac, y:maxmac, x:maxmac, w:maxmac, v:maxmac, v':maxmac, N:nonce ;
	event partSC(C, z, y) ==> partCS(S, T, x, y) && fullTC(C, S, N, v, v', z, w).

query z:maxmac, x:maxmac, y: maxmac, y':maxmac, w:maxmac;
       event fullCS(S,T,z,y,w) ==> fullSC(C,x,y',w).	 
*)

(* channels c1, c2, c3, c4, c5, c6, c9, c10, c18, c17, c19 *)



(*channels c21, c16,*)





(*channels c22, c23 *)



process 
	in(start, ());
new rkC: keyseed;
let pkC: pkey = pkgen(rkC) in
let skC: skey = skgen(rkC) in
new rkCs: skeyseed;
let spkC: spkey = spkgen(rkCs) in
let sskC: sskey = sskgen(rkCs) in
new rkK: keyseed;
let pkK: pkey = pkgen(rkK) in
let skK: skey = skgen(rkK) in
new rkKs: skeyseed;
let spkK: spkey = spkgen(rkKs) in
let sskK: sskey = sskgen(rkKs) in
new rkCA: skeyseed;
let pkCA: spkey = spkgen(rkCA) in
let skCA: sskey = sskgen(rkCA) in
new rKt: symkeyseed;
let Kt: key = kgen(rKt) in
new rKs: symkeyseed;
let Ks: key = kgen(rKs) in
new q1: sseed;
new q2: sseed;
let certC: signature = sign(concat1(C, pkC, spkC), skCA, q1) in
let certK: signature = sign(concat7(K, pkK, spkK), skCA, q2) in
out(c25, (pkC, spkC, spkK, pkCA));
((
  ! i_1 <= N
  in(c1[i_1], hostT: tgs);
  new n1_1: nonce;
  new n2_1: nonce;
  new r1: sseed;
  new tc0: timest;
  out(c2[i_1], (C, pkC, spkC, certC, tc0, n2_1, sign(padno(tc0, n2_1), sskC, r1), C, hostT, n1_1));
  in(c3[i_1], (m21: bitstring, =C, TGT_1: maxmac, m24: maxmac));
  let injbot1(concat2(hostZ: kas, pkZ: pkey, spkZ: spkey, ms1: signature, k3: key, k4: mkey, ck1: macs, ms2: signature)) = dec(m21, skC) in
  if check(concat7(hostZ, pkZ, spkZ), pkCA, ms1) then
  if check(concat3(k3, k4, ck1), spkK, ms2) then
  let y': maxmac = concat6(C, pkC, spkC, certC, tc0, n2_1, sign(padno(tc0, n2_1), sskC, r1), C, hostT, n1_1) in
  if checkmac(y', k4, ck1) then
  let injbot2(concat4(AK: key, =n1_1, tk: timest, =hostT)) = decsym(m24, k3) in
  event fullCK(hostZ, hostT, n1_1, m21, TGT_1, m24);
  out(c18[i_1], acceptC1(hostZ, hostT))
) | (
  ! i_2 <= N
  in(c22[i_2], (hostY: client, pkY: pkey, spkY: spkey, ms3: signature, tc1: timest, n4: nonce, ms4: signature, hostV: client, hostW: tgs, n5: nonce));
  if hostV = hostY then
  if check(concat1(hostY, pkY, spkY), pkCA, ms3) then
  if check(padno(tc1, n4), spkY, ms4) then
  find j1 = j1_1 <= N2 suchthat defined(Lhost[j1_1], Qkey[j1_1]) && (Lhost[j1_1] = hostW) then
  new s3: symkeyseed;
  let AK_1: key = kgen(s3) in
  new s1: symkeyseed;
  let k1: key = kgen(s1) in
  new s2: mkeyseed;
  let k2: mkey = mkgen(s2) in
  new tk_1: timest;
  new r4: symseed;
  let TGT_2: maxmac = encsym(concat5(AK_1, tk_1, hostY), Qkey[j1], r4) in
  new r5: symseed;
  let e24: maxmac = encsym(concat4(AK_1, n5, tk_1, hostW), k1, r5) in
  new r6: sseed;
  let y: maxmac = concat6(hostY, pkY, spkY, ms3, tc1, n4, ms4, hostY, hostW, n5) in
  let ck: macs = mac(y, k2) in
  let ms21: signature = sign(concat3(k1, k2, ck), sskK, r6) in
  new r7: seed;
  let e21: bitstring = enc(concat2(K, pkK, spkK, certK, k1, k2, ck, ms21), pkY, r7) in
  let m5: bitstring = (e21, hostY, TGT_2, e24) in
  event fullKC(hostY, hostW, n5, e21, TGT_2, e24);
  out(c23[i_2], m5)
) | (
  ! i_3 <= N2
  in(c21[i_3], (Lhost: tgs, Lkey: key));
  let Qkey: key = (if Lhost = T then Kt else Lkey)
) | (
  ! i_4 <= N2
  in(c16[i_4], (Mhost1: tgs, Mhost2: server, Mkey: key));
  let Pkey: key = (if (Mhost1 = T) && (Mhost2 = S) then Ks else Mkey)
))




(* EXPECTED
All queries proved.
0.544s (user 0.524s + system 0.020s), max rss 74272K
END *)
