(* Version in which the participants only talk to A and B 
   Using two series of function symbols to prevent repeated applications
   of the same cryptographic rule.
*)

param N.
param N2.
param N3.

type nonce [large,fixed].
type host.
type key [fixed].
type keyseed [fixed].
type seed [fixed].
type macs [fixed].

(* For simplicity, in this first example, I ignore the length of messages
   and the fact that decryption may fail. See otway-rees2Stream for a better
   version *)
(* Shared-key encryption (CPA) *)

proba Penc.

fun enc(bitstring, key, seed): bitstring.
fun kgen(keyseed):key.
fun dec(bitstring, key): bitstring.

fun enc2(bitstring, key, seed): bitstring.
fun kgen2(keyseed):key.

equation forall m:bitstring, r:keyseed, r2:seed; 
	dec(enc(m, kgen(r), r2), kgen(r)) = m.

const Z: bitstring.
equiv
  ! N3 new r: keyseed; ! N new r2: seed; gen_Oracle(x: bitstring) := return(enc(x, kgen(r), r2))
<=(N3 * Penc(time, N))=>
  ! N3 new r: keyseed; ! N new r2: seed; gen_Oracle(x: bitstring) := return(enc2(Z, kgen2(r), r2)).

(* Mac *)

proba Pmac.

fun mac(bitstring, key):macs.
fun check(bitstring, key, macs): bool.
fun mkgen(keyseed):key.

fun mac2(bitstring, key):macs.
fun check2(bitstring, key, macs): bool.
fun mkgen2(keyseed):key.

equation forall m:bitstring, r:keyseed;
	check(m, mkgen(r), mac(m, mkgen(r))).
equation forall m:bitstring, r:keyseed;
	check2(m, mkgen2(r), mac2(m, mkgen2(r))).

equiv
  ! N3 new r: keyseed; (
    ! N gen_Oracle_1(x: bitstring) := return(mac(x, mkgen(r))) |
    ! N2 gen_Oracle_2(m: bitstring, ma: macs) := return(check(m, mkgen(r), ma)))
<=(N3 * Pmac(time, N, N2))=>
  ! N3 new r: keyseed; (
    ! N gen_Oracle_1(x: bitstring) := return(mac2(x, mkgen2(r))) |
    ! N2 gen_Oracle_2(m: bitstring, ma: macs) := find j <= N suchthat defined(x[j]) && (m = x[j]) && check2(x[j], mkgen2(r), ma) then return(true) else return(false)).


(* Queries *)

query secret keyA.
query secret keyB.

channel c1, c2, c3, c4, c5, c6, c7, c8, start, dummy, finish.
const A : host.
const B : host.

event endA(host, host, nonce, key).
event endB(host, host, nonce, key).
event beginA(host, host, nonce).
event beginB(host, host, nonce).

query k: key, n: nonce, y: host, x: host;
  event(endA(x, y, n, k)) ==> event(beginB(x, y, n)) public_vars keyA, keyB.
query k: key, n: nonce, y: host, x: host;
  event(endB(x, y, n, k)) ==> event(beginA(x, y, n)) public_vars keyA, keyB.







	
process 
	in(start, =());
new rKas: keyseed;
let Kas: key = kgen(rKas) in
new rmKas: keyseed;
let mKas: key = mkgen(rmKas) in
new rKbs: keyseed;
let Kbs: key = kgen(rKbs) in
new rmKbs: keyseed;
let mKbs: key = mkgen(rmKbs) in
out(dummy, ());
((
  ! i_1 <= N
  in(start[i_1], =());
  new M_1: nonce;
  new Na_1: nonce;
  new s1: seed;
  let ea1: bitstring = enc((Na_1, M_1, A, B), Kas, s1) in
  event beginA(A, B, M_1);
  out(c1[i_1], (M_1, A, B, ea1, mac(ea1, mKas)));
  in(c2[i_1], (=M_1, ea2_1: bitstring, ma2_1: macs));
  if check(ea2_1, mKas, ma2_1) then
  let (=Na_1, ka: key) = dec(ea2_1, Kas) in
  event endA(A, B, M_1, ka);
  let keyA: key = ka
) | (
  ! i_2 <= N
  in(c3[i_2], (M_2: nonce, =A, =B, ea: bitstring, ma: macs));
  new Nb_1: nonce;
  new s2: seed;
  let e2: bitstring = enc((Nb_1, M_2, A, B), Kbs, s2) in
  event beginB(A, B, M_2);
  out(c4[i_2], (M_2, A, B, ea, ma, e2, mac(e2, mKbs)));
  in(c5[i_2], (=M_2, ea2_2: bitstring, ma2_2: macs, eb2: bitstring, mb2: macs));
  if check(eb2, mKbs, mb2) then
  let (=Nb_1, kb: key) = dec(eb2, Kbs) in
  out(c6[i_2], (M_2, ea2_2, ma2_2));
  in(finish[i_2], =());
  event endB(A, B, M_2, kb);
  let keyB: key = kb
) | (
  ! i_3 <= N
  in(c7[i_3], (M_3: nonce, =A, =B, ea1_1: bitstring, ma1: macs, eb1: bitstring, mb1: macs));
  if check(ea1_1, mKas, ma1) then
  let (Na_2: nonce, =M_3, =A, =B) = dec(ea1_1, Kas) in
  if check(eb1, mKbs, mb1) then
  let (Nb_2: nonce, =M_3, =A, =B) = dec(eb1, Kbs) in
  new k: key;
  new s3: seed;
  new s4: seed;
  let e1: bitstring = enc((Na_2, k), Kas, s3) in
  let e2_1: bitstring = enc((Nb_2, k), Kbs, s4) in
  out(c8[i_3], (M_3, e1, mac(e1, mKas), e2_1, mac(e2_1, mKbs)))
))



(* EXPECTED
All queries proved.
0.180s (user 0.152s + system 0.028s), max rss 47776K
END *)
