
(** ElGamal encryption scheme 
    Modeling by Bruno Blanchet and Avik Chaudhuri **)

(** An encryption scheme is shown to be SS
    under the DDH assumption.
    See Section 3, "Sequences of Games", Victor Shoup. **)

(* Replicated the encryption oracle, so that several encryptions can
be performed, in order to show that El Gamal is IND-CPA. This goes 
slightly beyond the proof in Shoup's paper (it is however an easy
consequence of Shoup's proof). *) 

param n, qE.

type Z [fixed].
type G [fixed].

(* Decisional Diffie-Hellman assumption *)

proba pDDH.

expand DDH(G,Z,gamma,exp,mult,pDDH).

(* Product in the group G *)

fun dot(G,G): G.

(* dot(.,y) is a uniform function *)

equiv
  ! n new x: G; gen_Oracle(y: G) := return(dot(x,y)) [all]
<=(0)=>
  ! n new x: G; gen_Oracle(y: G) := return(x).

(* query *)
query secret b_1.

channel cPK, cE, cEret, start.



process
  in(start, ());
new b_1: bool;
new x_1: Z;
let alpha: G = exp(gamma, x_1) in
out(cPK, alpha);
! i_1 <= qE
in(cE[i_1], (m0: G, m1: G));
let m_1: G = (if b_1 then m1 else m0) in
new y_1: Z;
let beta: G = exp(gamma, y_1) in
let delta: G = exp(alpha, y_1) in
let zeta: G = dot(delta, m_1) in
out(cEret[i_1], (beta, zeta))



(* EXPECTED
All queries proved.
0.032s (user 0.028s + system 0.004s), max rss 10828K
END *)
