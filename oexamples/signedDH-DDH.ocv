(* Signed Diffie-Hellman protocol *)

proof { 
crypto uf_cma(sign) rkA;
crypto uf_cma(sign) rkB;
crypto ddh(exp);
auto
}

param N, N2.

type host.
type keyseed [large,fixed].
type seed [fixed].
type pkey [bounded].
type skey [bounded].
type blocksize.
type signature.

type Z [large,bounded].
type G [large,bounded].
type D [fixed].

(* the DDH assumption *) 

proba pDDH.

expand DDH(G, Z, g, exp, mult, pDDH).

(* h entropy extractor *)

proba pH.
fun h(G):D.

equiv foreach i<=N do x <-R G; OH() := return(h(x))
<=(N * pH(time + (N-1)*time(h)))=>
      foreach i<=N do X <-R D; OH() := return(X).

(* signatures *)

fun concatA(host, host, G, G):blocksize [compos].
fun concatB(host, host, G, G):blocksize [compos].
forall x:host, y:host, z:G, t:G, x':host, y':host, z':G, t':G;
   concatA(x,y,z,t) <> concatB(x',y',z',t').
    
proba Psign.
proba Psigncoll.

expand UF_CMA_signature(keyseed, pkey, skey, blocksize, signature, seed, skgen, pkgen, sign, check, Psign, Psigncoll).

const A,B:host.

(* query *)
query secret keyA.
query secret keyB.

event endA(host, host, G, G).
event beginB(host, host, G, G).
event endB(host, host, G, G).

query x:G, y:G; event inj:endA(A, B, x, y) ==> inj:beginB(A, B, x, y).
query x:G, y:G; event inj:endB(A, B, x, y) ==> inj:endA(A, B, x, y).

let processA = 
  OA1(hostB: host) :=
  a <-R Z;
  ga <- exp(g,a);
  return(A, hostB, ga);
  OA2(=A, =hostB, gb:G, s:signature) :=
  find j2 <= N2 suchthat defined(Khost[j2],Rkey[j2]) && (Khost[j2] = hostB) then
  pkhostB <- Rkey[j2];
  if check(concatB(A, hostB, ga, gb), pkhostB, s) then
  r2 <-R seed;
  gab <- exp(gb, a);
  kA <- h(gab);
  event endA(A, hostB, ga, gb);
  return(sign(concatA(A, hostB, ga, gb), skA, r2));
  (* OK *)
  OA3() :=
  if hostB = B then 
  (keyA:D <- kA)
  else
   return(kA).

let processB =
  OB1(hostA:host, =B, ga:G) :=
  b <-R Z;
  gb <- exp(g,b);
  r1 <-R seed;
  event beginB(hostA, B, ga, gb);
  return(hostA, B, gb, sign(concatB(hostA, B, ga, gb), skB, r1));
  OB2(s:signature) :=
  find j2 <= N2 suchthat defined(Khost[j2],Rkey[j2]) && (Khost[j2] = hostA) then
  pkhostA <- Rkey[j2];
  if check(concatA(hostA, B, ga, gb), pkhostA, s) then
  gab <- exp(ga, b);
  kB <- h(gab);
  event endB(hostA, B, ga, gb);
  (* OK *)
  if hostA = A then
  (keyB:D <- kB)
  else
   return(kB).

let processK =
  foreach iK <= N2 do
  OK(Khost: host, Kkey: pkey) :=
  Rkey:pkey <-
		if Khost = B then pkB else
		if Khost = A then pkA else
		Kkey.

process
	Ostart() :=
	rkA <-R keyseed;
	skA <- skgen(rkA);
	pkA <- pkgen(rkA);
	rkB <-R keyseed;
	skB <- skgen(rkB);
	pkB <- pkgen(rkB);
	return(pkA, pkB);
	((foreach iA <= N do processA) | (foreach iB <= N do processB) | processK)


(* EXPECTED
All queries proved.
0.200s (user 0.190s + system 0.010s), max rss 36160K
END *)
