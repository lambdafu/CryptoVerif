(*

Woo and Lam public-key auth protocol (1992, computer)

A -> B: A
B -> A: N
A -> B: { N }_skA
B -> S: A
S -> B: A, { A, pkA }_skS

Attack
Full agreement is true, but there is an attack !
(Indeed, the messages never mention B.)

Session identifier = (A, N, { N }_skA)
Authentification in one direction only.

*)

param N.
param N2.
param N3.
param N4.

type nonce [large,fixed].
type host [bounded].
type skey [bounded].
type pkey [bounded].
type keyseed [large,fixed].
type seed [fixed].
type signature [bounded].
type blocksize [fixed].
type blocksizebot [bounded].

fun pad(nonce):blocksize [compos].
fun concat2(host, pkey):blocksize [compos].

forall z:nonce, x2:host, y2:pkey;
	pad(z) <> concat2(x2,y2).

(* Signatures *)

proba Psign.
proba Psigncoll.

expand SUF_CMA_signature(keyseed, pkey, skey, blocksize, signature, seed, sskgen, spkgen, sign, check, Psign, Psigncoll).

(* Queries *)

channel c1, c2, c3, c4, c5, c6, c7, c8, c9, c10, c11, c12, c13, c14,
	start, finish.

const A:host.
const B:host.
fun acceptA(host):bitstring.
fun acceptB(host):bitstring.

event fullA(host, bitstring).
event fullB(host, bitstring).

query y:bitstring;
	event inj:fullB(A,y) ==> inj:fullA(B,y).

let processA =
	in(c1, hostB0:host);
	out(c2, A);
	in(c3, n:nonce);
	new r1:seed;
	let m3 = sign(pad(n), skA, r1) in
	event fullA(hostB0, (A, n, m3));
	out(c4, (m3, acceptA(hostB0))).

let processB =
	in(c5, hostA0:host);
	new n:nonce;
	out(c6, n);
	in(c7, m:signature);
	out(c8, hostA0);
	in(c9, (=hostA0, pkA2:pkey, m4:signature));
	if check(concat2(hostA0, pkA2), pkS, m4) then
	if check(pad(n), pkA2, m) then
	(* OK *)
	event fullB(hostA0, (hostA0, n, m));
        out(c14, acceptB(hostA0)).


let processS =
	in(c10, hostA:host);
	find j2 <= N2 suchthat defined(Khost[j2],Rkey[j2]) && (Khost[j2] = hostA) then
	let pka = Rkey[j2] in
	new r2:seed;
	out(c11, (hostA, pka, sign(concat2(hostA, pka), skS, r2))).

let processK =
	in(c12, (Khost: host, Kkey: pkey));
	let Rkey:pkey = 
		if Khost = A then pkA else
		if Khost = B then pkB else
		Kkey.

process 
	in(start, ());
	new rkA: keyseed;
	let pkA = spkgen(rkA) in
	let skA = sskgen(rkA) in
	new rkB: keyseed;
	let pkB = spkgen(rkB) in
	let skB = sskgen(rkB) in
	new rkS: keyseed;
	let pkS = spkgen(rkS) in
	let skS = sskgen(rkS) in
	out(c13, (pkA, pkB, pkS));
	((! N processA) |
	 (! N processB) | 
	 (! N processS) |
	 (! N2 processK))


(* EXPECTED
RESULT Could not prove event inj:fullB(A, y) ==> inj:fullA(B, y).
0.080s (user 0.070s + system 0.010s), max rss 28160K
END *)
