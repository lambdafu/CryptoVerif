
(** Chosen ciphertext secure symmetric encryption **)

(** The construction of an encryption scheme 
    using a pseudo-random function family
    and an unpredictable function family
    is shown to be IND-CCA2.
    See Section 7, "Sequences of Games", Victor Shoup. **)

param n, m, p, q, qE, qD.

type seed [large,fixed].
type S [large,fixed].
type K [large,fixed].

type N [large,fixed].
type L [fixed].
type W [fixed].

proba pPRF.
proba pUF.
proba pXOR.

(* f(S) is a pseudo-random function family *) 

expand PRF(seed, S, N, L, kgen, f, pPRF).

(* h(K) is a message authentication code (an unpredictable function family) *)

fun h(K,N,L): W.
fun h'(K,N,L): W.

equiv 
    foreach i <= n do k <-R K; (
         foreach im <= m do Omac(y:N,z:L) := return(h(k,y,z)) |
	 foreach ip <= p do Ocheck(y':N,z':L,t:W) := return(t = h(k,y',z'))
    ) 
<=(pUF(time,n,m,p))=>
    foreach i <= n do k <-R K; (
         foreach im <= m do Omac(y:N,z:L) := return(h'(k,y,z)) |
	 foreach ip <= p do Ocheck(y':N,z':L,t:W) := find u <= m suchthat 
	     defined(y[u],z[u]) && y' = y[u] && z' = z[u] 
                          then return(t = h'(k,y',z')) else return(false)
    ).

fun xor(L,L): L.

(* the theory of xor implies several propositions *)

forall x:L, y:L; xor(x, xor(x,y)) = y.

equiv 
    foreach i <= n do x <-R L; OX(y:L) := return(xor(x,y))
<=(0)=>
    foreach i <= n do x <-R L; OX(y:L) := return(x).

fun choose(bool,L,L): L.

(* choose(b,M,N) = if b then M else N *)

forall x:bool, y:L, z:L;
  (x && choose(x,y,z) = y) || (not(x) && choose(x,y,z) = z).

const reject:L.

(* query *)
query secret b.

let processE = 
    foreach iE <= qE do
    OE(m0:L, m1:L) :=
    x' <-R N;
    let tempE = f(s,x') in
    let m' = choose(b,m1,m0) in
    let c':L = xor(tempE, m') in
    let t':W = h(k,x',c') in
    return(x',c',t').

let processD = 
    foreach iD <= qD do
    OD(x:N, c:L, t:W) :=
    if t = h(k,x,c) 
      then 
        (find u <= qE suchthat 
                defined(x'[u],c'[u],t'[u])
             && x = x'[u]
             && c = c'[u]
	     && t = t'[u] (* BB: added *)
           then return(reject)  
           else 
             let tempD = f(s,x) in 
             let m = xor(tempD, c) in 
             return(m) ) 
      else return(reject).

process
  foreach i <= q do
  Ostart() :=
  k <-R K;
  sseed <-R seed;
  let s = kgen(sseed) in
  b <-R bool;
  return(); 
  (processE | processD)

(* EXPECTED
All queries proved.
0.030s (user 0.030s + system 0.000s), max rss 19552K
END *)
