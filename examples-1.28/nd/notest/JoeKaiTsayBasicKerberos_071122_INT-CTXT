(* 3 rounds of Basic Kerberos, consider only authentication properties *)

(* version with keyderivation used for each encryption.
   This version currently leads to using too much resources.
   We need common subexpression elimination to solve this problem *)

set interactiveMode = true.

param N.
param N2.
param N3.
param N4.

type nonce [large,fixed].
type kas [bounded].
type client [bounded].
type tgs [bounded].
type server [bounded].
type mkey [bounded].
type mkeyseed [fixed].
type key [fixed].
type keyseed [fixed].
type protkey [fixed].
type usenum [fixed].
type seed [fixed].
type macs [fixed].
type maxenc [bounded].
type timest [fixed].							

(* following functions are needed to set the type of a list of elements to `bitstring', so that the list can be encrypted: *)
fun concat1(protkey, nonce, timest, tgs):maxenc [compos]. 		(* needed for {AK, n1, t_K, T}_Kc*)
fun concat2(protkey, timest, client):maxenc [compos].  		(* needed for TGT and ST *)
fun concat3(protkey, nonce, timest, server):maxenc [compos].       	(* neededfor {SK, n2, t_T, S}_AK *)
fun pad(client, timest):maxenc [compos].
fun padts(timest): maxenc [compos].

forall y:protkey, x:timest, z:client, t2:protkey, y2:nonce, x2:timest, z2:tgs;		concat2(y,x,z) <> concat1(t2,y2,x2,z2).
forall t:protkey, y:nonce, x:timest, z:server, t2:protkey, y2:nonce, x2:timest, z2:tgs; concat3(t,y,x,z) <> concat1(t2,y2,x2,z2).
forall z:client, t:timest, t2:protkey, y2:nonce, x2:timest, z2:tgs;  	   pad(z,t) <> concat1(t2,y2,x2,z2).
forall t:timest, t2:protkey, y2:nonce, x2:timest, z2:tgs;   		   padts(t) <> concat1(t2,y2,x2,z2).
forall y:protkey, x:timest, z:client, t2:protkey, y2:nonce, x2:timest, z2:server; concat2(y,x,z) <> concat3(t2,y2,x2,z2).
forall y:protkey, x:timest, z:client, t2:timest, z2:client;    		   concat2(y,x,z) <> pad(z2,t2). 
forall y:protkey, x:timest, z:client, t2:timest;    			   concat2(y,x,z) <> padts(t2). 
forall t:protkey, y:nonce, x:timest, z:server, t2:timest, z2:client;  	   concat3(t,y,x,z) <> pad(z2,t2).
forall t:protkey, y:nonce, x:timest, z:server, t2:timest;  			   concat3(t,y,x,z) <> padts( t2).
forall t:timest, z:client, t2:timest;  					   pad(z,t)<> padts(t2).


(* Shared-key encryption (CPA Stream cipher) *)

proba Penc.
proba Pencctxt.

fun enc(maxenc,key,seed): bitstring.
fun kgen(keyseed): key.
fun dec(bitstring,key): bitstringbot.

fun enc2(maxenc,key,seed): bitstring.
fun kgen2(keyseed): key.

fun injbot(maxenc): bitstringbot [compos].
forall x:maxenc; injbot(x) <> bottom.

(* The function Z returns for each bitstring, a bitstring of the same length, consisting only of zeroes. *)
fun    Z(maxenc):maxenc.
const  Zconcat1:maxenc.
const  Zconcat2:maxenc.
const  Zconcat3: maxenc.
const  Zpad:maxenc.
const  Zpadts: maxenc.

forall m:maxenc, r:keyseed, r2:seed; 
	dec(enc(m,kgen(r),r2),kgen(r)) = injbot(m).
forall y:nonce, x:timest, z:tgs, t:protkey;
	Z(concat1(t,y,x,z)) = Zconcat1.
forall y:protkey, x:timest, z:client; 
	Z(concat2(y,x,z)) = Zconcat2.
forall  y:nonce, x:timest, z:server, t:protkey;
	Z(concat3(t,y,x,z)) = Zconcat3.
forall z:client, t:timest;
        Z(pad(z,t)) = Zpad.
forall t: timest;
        Z(padts(t)) = Zpadts.
					    

equiv  !N2 new r: keyseed; ((x:maxenc) N -> new r2: seed; enc(x, kgen2(r), r2)) 
     <=(N2 * Penc(time, N))=> 
       !N2 new r: keyseed; ((x:maxenc) N -> new r2: seed; enc2(Z(x), kgen2(r), r2)).


equiv ! N2 new r: keyseed; ((x:maxenc) N -> new r2: seed; enc(x, kgen(r), r2),
			    (y:bitstring) N3 -> dec(y,kgen(r))) 
     <=(N2 * Pencctxt(time, N, N3))=> 
      ! N2 new r: keyseed; ((x:maxenc) N -> new r2: seed; let z:bitstring = enc(x, kgen2(r), r2) in z,
			    (y:bitstring) N3 -> find j <= N suchthat defined(x[j],r2[j],z[j]) && z[j] = y then injbot(x[j]) else bottom).



(* Pseudorandom function for key derivation *)
proba pPRF.

fun keyderivation(protkey, usenum):key.

equiv !N2 new z:protkey;
      	 (x:usenum) N -> keyderivation(z,x)
      <=(N2 * pPRF(time, N))=>
      !N2 (x:usenum) N -> find u <= N suchthat defined(x[u],r[u]) && x = x[u]
                 then r[u]
                 else (*new r:D2; r.*) new s:keyseed; let r:key=kgen(s) in r. 

(* Key usage numbers
   IMPORTANT NOTE: by default, CryptoVerif considers that if you declare 
   several constants, then these constants are different.
   If you like, it would also be possible to change this setting
   and say explicitly that un1 <> un2.
 *)

const un1: usenum.
const un2: usenum.
const un3: usenum.
const un4: usenum.
const un5: usenum.
const un6: usenum.
const un7: usenum.
const un8: usenum.
const un9: usenum.
const un10: usenum.
const un11: usenum.
const un12: usenum.

(* Queries *)

channel c1, c2, c3, c4, c5, c6, c7, c8, c9, c10, c11, c12, c13, c14, c15, c16, c17, c18, c19,c20, c21, c22, c23, c24, c25, start, finish, cC, cT.
const C : client.
const K : kas.
const T : tgs.
const S : server.						

fun acceptC1(kas): bitstring.
fun acceptC2(tgs,server): bitstring.
fun acceptC3(server): bitstring.
fun acceptT(client,server): bitstring.
fun acceptS(client): bitstring.

event fullKC(client, tgs, nonce, bitstring, bitstring).
event fullCK(tgs, nonce, bitstring, bitstring).		

event partCT(tgs, bitstring, bitstring).
event fullCT(tgs, server, nonce, bitstring, bitstring, bitstring). 			
event partTC(client, bitstring, bitstring).    		
event fullTC(client, server, nonce, bitstring, bitstring, bitstring, bitstring).
			
event partCS(server, tgs, bitstring, bitstring).
event fullCS(server, tgs, bitstring, bitstring, bitstring).
event partSC(client, bitstring, bitstring).
event fullSC(client, bitstring, bitstring, bitstring).      		


(* authentication queries : *)

query x:bitstring, y:bitstring, z:bitstring, N:nonce;
       event inj:fullCK(T,N,x,y) ==> inj:fullKC(C,T,N,z,y).
		 
query x:bitstring, x':bitstring, y:bitstring, y':bitstring, N:nonce;					   
        event partTC(C,x,y) ==> partCT(T,x',y) && fullKC(C,T,N,x,y').
						  	
query z:bitstring, z':bitstring, y:bitstring, x:bitstring, x':bitstring, v:bitstring, N:nonce;
      	event inj:fullCT(T,S,N,x,z,y) ==> inj:fullTC(C,S,N,x',z',v,y).

query z:bitstring, y:bitstring, x:bitstring, w:bitstring, v:bitstring, v':bitstring, N:nonce ;
	event partSC(C,z,y) ==> partCS(S,T,x,y) && fullTC(C,S,N,v,v',z,w).

query z:bitstring, x:bitstring, y:bitstring, y':bitstring, w:bitstring;
       event fullCS(S,T,z,y,w) ==> fullSC(C,x,y',w).
	 

(* processC,C2,C3 use channels c1, c2, c3, c4, c6, c9, c10, c17, c18, c19, c20 *)
let processC = 
	in(c1, hostT:tgs);						(* hostT = T  *) 
	new n1:nonce;
	out(c2, (C, hostT, n1));
	in(c3, (=C, m:bitstring, m2:bitstring));			(* m = Ticket Granting Ticket *)
        let Kc3 = keyderivation(Kc, un3) in
	let injbot(concat1(AK, =n1, tk, =hostT)) = dec(m2, Kc3 (*Kc*)) in 
	event fullCK(hostT, n1, m, m2);
	out(c18, acceptC1(K));
	(! N3 processC2 ).

let processC2 =
    	in(c17, (hostT':tgs, hostS:server));				(* hostS = S *)
	if hostT' = hostT then
	new n3:nonce;
	new tc:timest;
	new r1:seed;
	let AK4 = keyderivation(AK, un4) in
	let e5 = enc(pad(C, tc), AK4, r1) in				(* e5 = {C, t_C}_AK *)
	event partCT(hostT, m, e5);	
	out(c4, (m, e5, hostS, n3));
	in(c5, (=C, m6:bitstring, m7:bitstring));
	let AK8 = keyderivation(AK, un8) in
	let injbot(concat3(SK, =n3, tt, =hostS))= dec(m7, AK8) in	(* m7 = {SK, n2, t_T, S}_AK   *)
	event fullCT(hostT, hostS, n3, m, e5, m7);
	out(c19, acceptC2(hostT, hostS));
	(! N4 processC3).

let processC3 =					
    	in(c20, hostS':server);
	if hostS' = hostS then      
	new tc':timest;
	new r2:seed;
	let SK11 = keyderivation(SK, un11) in
	let e12 = enc(pad(C, tc'), SK11, r2) in				(* e12 = {C, t'_C}_SK *)
	event partCS(hostS, hostT, m7, e12);				(* m7 = {SK, n2, t_T S}_AK *)
	out(c6, (m6, e12));
	in(c9, (m13:bitstring));
	let SK12 = keyderivation(SK, un12) in
	if injbot(padts(tc'))= dec(m13, SK12) then
	event fullCS(hostS, hostT, m7, e12, m13);			(* m13 = {t}_SK *)
	out(c10, acceptC3(hostS)).
		
(* processCKEY,TKEY,SKEY use channels c13, c16, c21 *)
let processCKEY =
	in(c13, (Khost:client, Kkey:protkey));
	let Rkey:protkey = 
		if Khost = C then Kc else
		Kkey.

let processTKEY =
	in(c21, (Lhost:tgs, Lkey:protkey)); 
	let Qkey:protkey =
	    	  if Lhost = T then Kt else
		  Lkey.

let processSKEY = 
	in(c16,(Mhost1:tgs, Mhost2:server, Mkey:protkey));
	let Pkey:protkey = 
	    	 if Mhost1 = T && Mhost2 = S then Ks else
		 Mkey.

					
(* processK uses channels c14, c15 *)
let processK = 
	in(c14, (hostY:client, hostW:tgs, n:nonce));
	find j1 <= N2 suchthat defined(Khost[j1],Rkey[j1]) && (Khost[j1] = hostY) then        (* shared key KC *)
	find j2 <= N2 suchthat defined(Lhost[j2],Qkey[j2]) && (Lhost[j2] = hostW) then        (* shared key KT *)
	(*new AK:keyseed;
	let AK = kgen(rAK) in*)
	new AK: protkey;
	new tk:timest;
	new r3:seed;
	let kQ = keyderivation(Qkey[j2], un2) in
	let e3 = enc(concat2(AK, tk, hostY), kQ (*Qkey[j2]*), r3) in					(* e3 = TGT *)
	new r4:seed;
	let kR = keyderivation(Rkey[j1], un3) in
	let e4 = enc(concat1(AK, n, tk, hostW), kR (*Rkey[j1]*), r4) in
	event fullKC(hostY, hostW, n, e3, e4);
	out(c15, (hostY, e3, e4)).

(* processT uses channels c7, c8 *)
let processT =
    	in(c7, (m8:bitstring, m9:bitstring, hostW:server, n': nonce));				(* hostW = S *)
	let Kt2 = keyderivation(Kt, un2) in
	let injbot(concat2(AK, tk, hostY)) = dec(m8, Kt2) in					(* hostY = C *)
	let AK4 = keyderivation(AK, un4) in
	let injbot(pad(=hostY, ts)) = dec(m9, AK4) in
	event partTC(hostY, m8, m9);								(* m8 = TGT={AK, C}_Kt and m9 = {C,t}_AK  *)
	find j3 <= N2 suchthat defined(Mhost1[j3],Mhost2[j3],Pkey[j3]) && (Mhost1[j3] = T && Mhost2[j3] = hostW) then        (* shared key Ks *)	
	(*new rSK:keyseed;
	let SK= kgen(rSK) in*)
	new SK: protkey;
	new tt:timest;
	new r10:seed;
	let kP = keyderivation(Pkey[j3], un2) in
	let e10 = enc(concat2(SK, tt, hostY), kP (*Pkey[j3]*), r10 ) in   
	new r11:seed;
	let AK8 = keyderivation(AK, un8) in
	let e11 = enc(concat3(SK, n', tt, hostW), AK8, r11) in
	event fullTC(hostY, hostW, n', m8, m9, e10, e11);    (* e10 = ST = {SK, C}_Ks, e11 = {SK, n2, S}_AK *)
	out(c8, (hostY, e10, e11, acceptT(hostY,hostW))).	

(*channels c11, c12*)	
let processS=
	in(c11, (m14: bitstring, m15:bitstring));
	let Ks2 = keyderivation(Ks, un2) in
	let injbot(concat2(SK, tt, hostC)) = dec(m14, Ks2) in		(*hostC = C *)
	let SK11 = keyderivation(SK, un11) in
	let injbot(pad(=hostC, tc')) = dec(m15, SK11) in
	new r16:seed;
	let SK12 = keyderivation(SK, un12) in
	let e16 = enc(padts(tc'), SK12, r16) in
	event partSC(hostC, m14, m15);					(*m14 = {SK,C}_Ks, m15 = {C, t}_SK *)
	event fullSC(hostC, m14, m15, e16);
	out(c12, (e16, acceptS(hostC))).


process
	in(start, ());
	(*new rKc: keyseed;
	let Kc = kgen(rKc) in*)
	new Kc: protkey;					(* enc key between client C and KAS K *)
	(*new rKt: keyseed;
	let Kt = kgen(rKt) in*)
	new Kt: protkey;					(* enc key between TGS T and KAS K *)
	(*new rKs: keyseed;
	let Ks = kgen(rKs) in*)
	new Ks: protkey;
	out(c25, ());
	((! N processC) | 
	 (! N processK) |
	 (! N processT) |
	 (! N processS)	|
	 (! N2 processCKEY)|
	 (! N2 processTKEY)|
	 (! N2 processSKEY))

