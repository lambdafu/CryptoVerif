(**********************************************************************
*
* Prove the chosen-ciphertext security of counter-mode-then-MAC.
*
*  * Cryptoverif version: 1.18
*  * Blocks: 2
*  * Result: success
*
*
***********************************************************************)



type block [fixed, large].
type aes_keyseed [fixed, large, bounded].
type aes_key [large, bounded].
proba Paes.
expand PRP_cipher(aes_keyseed, aes_key, block, aes_kgen, aes_enc, aes_dec, Paes).


(* Temporarly choosing a separate nonce for each block *)
type cm_ciphertext [bounded].
fun cm_join(block, block, block): cm_ciphertext [compos].

type mac_keyseed [bounded, fixed, large].
type mac_key [large, bounded].
type macs [fixed, large].
proba Pmac.
expand SUF_CMA_mac(mac_keyseed, mac_key, cm_ciphertext, macs, mac_keygen, mac, mac_check, Pmac).


type maced_ciphertext [bounded].
fun mac_join(cm_ciphertext, macs): maced_ciphertext [compos].

(* define xor to exist over blocks *)


expand Xor(block, xor, zero_block).


(* Useful helper function. Dunno why. *)
fun test(bool, block, block):block.
forall x:block,y:block; test(true,x,y) = x.
forall x:block,y:block; test(false,x,y) = y.

(* axiomitize incr *)
fun incr(block): block [compos, uniform].
forall x:block; incr(x) <> x.
(*
forall x:block; incr(incr(x)) <> x.
forall x:block; incr(incr(x)) <> incr(x).
forall x:block; incr(incr(incr(x))) <> x.
forall x:block; incr(incr(incr(incr(x)))) <> x.
forall x:block; incr(incr(incr(incr(incr(x))))) <> x.
*)
collision x <-R block; y <-R block; return(incr(x) = y) <=( 1/|block| )=> return(false). 


(* Parameters *)

param N. (* the number of times the adversary gets to call the encryption oracle *)
param C. (* the number of challenge ciphertexts available to the adversary *)

(* what do we want? That the adversary does not know which plaintext 
   was encrypted *)

query secret secret_bit.



let enc_oracle = 
    Oenc(b1 : block, b2 : block):=

       nonce1 <-R block;
       counter1 <- incr(nonce1);
       pad1 <- aes_enc(counter1, aesk);
       cipherblock1 <- xor(b1, pad1);

       counter2 <- incr(counter1);
       pad2 <- aes_enc(counter2, aesk);
       cipherblock2 <- xor(b2, pad2);

       cm_ctext <- cm_join(nonce1, cipherblock1, cipherblock2);
       mac_tag <- mac(cm_ctext, mack);
       
       maced_ctext <- mac_join(cm_ctext, mac_tag);    

       return (maced_ctext).



let challenge_oracle =
       
       Ochallenge(b11: block, b12:block, 
       		  b21: block, b22: block) :=

       b1 <- test(secret_bit, b11, b21);
       b2 <- test(secret_bit, b12, b22);

       (* le sigh-- cut and copy from encryption oracle *)

       nonce1 <-R block;
       counter1 <- incr(nonce1);
       pad1 <- aes_enc(counter1, aesk);
       cipherblock1 <- xor(b1, pad1);

       counter2 <- incr(counter1);
       pad2 <- aes_enc(counter2, aesk);
       cipherblock2 <- xor(b2, pad2);

       (*******************************

       Note: problem with next line 

       **********************************)
       cm_ctext <- cm_join(nonce1, cipherblock1, cipherblock2);
       mac_tag <- mac(cm_ctext, mack);
       
       maced_ctext <- mac_join(cm_ctext, mac_tag);

       (* end cut and copy from encryption oracle *)

       challenge : maced_ciphertext <- maced_ctext;

       return(challenge).




let dec_oracle = 
   Odec(maced_ctext: maced_ciphertext) :=
   find 
       u <= C suchthat defined(challenge[u]) && (maced_ctext = challenge[u])
   then
       end
   else
	let mac_join(cm_ctext, mac_tag) = maced_ctext in      
	if 
      	   mac_check(cm_ctext, mack, mac_tag)
   	then 
       	   let cm_join(nonce1, cipherblock1, cipherblock2) = cm_ctext in

 	   counter1 <- incr(nonce1);
       	   pad1 <- aes_enc(counter1, aesk);
       	   plainblock1 <- xor(pad1, cipherblock1);

 	   counter2 <- incr(counter1);
       	   pad2 <- aes_enc(counter2, aesk);
       	   plainblock2 <- xor(pad2, cipherblock2);

       	   return(plainblock1, plainblock2).


process 
	Ostart() :=
	(* Choose the key *)
	aesx <-R aes_keyseed;
        aesk <- aes_kgen(aesx);

	macx <-R mac_keyseed;
	mack <- mac_keygen(macx);

        secret_bit <-R bool;

	(* Make encryption oracles available now IN PARALLEL 
 	   with the rest of the game. *)
	return;

        (foreach i <= N do enc_oracle) | (foreach i <= N do dec_oracle) | (foreach i <= C do challenge_oracle)

