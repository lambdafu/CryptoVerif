(**********************************************************************
*
* Prove the chosen-plaintext security of MAC-and-counter-mode.
*
*  * Cryptoverif version: 1.18
*  * Blocks: 1
*  * Result: failure, as expected-- as implemented below, this is insecure.
*    In particualr, the MAC tag could leak information about the plaintext.
*
***********************************************************************)




type block [fixed, large].

type aes_keyseed [fixed, large].
type aes_key [bounded].
proba Paes.
expand PRP_cipher(aes_keyseed, aes_key, block, aes_kgen, aes_enc, aes_dec, Paes).


type mac_keyseed [ fixed, large].
type mac_key [bounded].
proba Pmac.
expand UF_CMA_mac(mac_keyseed, mac_key, block, block, mac_keygen, mac, mac_check, Pmac).



(* Useful helper function. Dunno why. *)

fun test(bool, block, block):block.
forall x:block,y:block; test(true,x,y) = x.
forall x:block,y:block; test(false,x,y) = y.



(* define xor to exist over blocks *)

expand Xor(block, xor, zero_block).


(* Parameters *)

param N. (* the number of times the adversary gets to call the encryption oracle *)


(* what do we want? That the adversary does not know which plaintext 
   was encrypted *)

query secret secret_bit.



let enc_oracle = 
    Oenc(b1 : block):=

       mac_tag <- mac(b1, mack);

       nonce <-R block;
       pad <- aes_enc(nonce, aesk);
       ciphertext <- xor(b1, pad);
       
       return (nonce, ciphertext, mac_tag).


let challenge_oracle =
       
       Ochallenge(b11: block, b21: block) :=
       secret_bit <-R bool;
       b1 <- test(secret_bit, b11, b21);

       (* le sigh-- cut and copy from encryption oracle *)


       mac_tag <- mac(b1, mack);

       nonce <-R block;
       pad <- aes_enc(nonce, aesk);
       ciphertext <- xor(b1, pad);
              
       challenge <- (nonce, ciphertext, mac_tag);
       (* end cut and copy from encryption oracle *)


       return(challenge).



process 
	Ostart() :=
	(* Choose the key *)
	aesx <-R aes_keyseed;
        aesk <- aes_kgen(aesx);

	macx <-R mac_keyseed;
	mack <- mac_keygen(macx);

	(* Make encryption oracles available now IN PARALLEL 
 	   with the rest of the game. *)
	return;

        (foreach i <= N do enc_oracle) | challenge_oracle