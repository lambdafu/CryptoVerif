(* This file proves that, when the master secret ms is a fresh random value,
  log_4 -> derive-secret(ms, client_ats, log_4)
        || derive-secret(ms, server_ats, log_4)
        || derive-secret(ms, exporter_ms, log_4)
and
  log_7 -> derive-secret(ms, resumption_ms, log_7)
are indistinguishable from independent random functions. *)


type extracted [large, fixed].
type key [large, fixed].
type three_keys [large, fixed].
type label.

param N, N', N3.

(* HMAC is a PRF *)

proba Pprf.
expand PRF(extracted, bitstring, key, HMAC, Pprf).

(* Hash is a collision resistant hash function *)

type hashkey [large,fixed].
type hash [fixed].
proba Phash.
expand CollisionResistant_hash(hashkey, bitstring, hash, Hash, Hash_oracle, Phash).

(* Derive_secret(Secret, Label, Messages) =
   HKDF_expand_label(Secret, Label, Hash(Messages), Hash.length) =
   HKDF_expand(Secret, [Hash.length, "TLS 1.3, " + Label, Hash(Messages)], Hash.length) =
   HMAC(Secret, [Hash.length, "TLS 1.3, " + Label, Hash(Messages), 0x00])

We define build_arg(Label, Hash) = [Length, "TLS 1.3, " + Label, Hash, 0x00].
*)

fun build_arg(label, hash): bitstring [data].

letfun HKDF_expand_label(Secret: extracted, Label: label, HashValue: hash) =
       HMAC(Secret, build_arg(Label, HashValue)).

letfun Derive_secret(hk: hashkey, Secret: extracted, Label: label, Messages: bitstring) = 
       HKDF_expand_label(Secret, Label, Hash(hk, Messages)).


fun concat(key, key, key): three_keys [data].

equiv(concat)
      !N new r1: key; new r2: key; new r3: key; O() := return(concat(r1, r2, r3))
      <=(0)=>
      !N new r: three_keys; O() := return(r).

(* Labels *)

const client_ats : label. (* ats = application traffic secret *)
const server_ats : label.
const exporter_ms : label. (* ms = master secret *)
const resumption_ms : label.

(* Concatenation of client_traffic_secret_0, server_traffic_secret_0, and exporter_secret *)

letfun Derive_Secret_cs_ats_exp(hk: hashkey, MasterSecret: extracted, log: bitstring) =
       concat(Derive_secret(hk, MasterSecret, client_ats, log),
              Derive_secret(hk, MasterSecret, server_ats, log),
	      Derive_secret(hk, MasterSecret, exporter_ms, log)).

(* resumption_secret *)

letfun Derive_Secret_rms(hk: hashkey, MasterSecret: extracted, log: bitstring) =
       Derive_secret(hk, MasterSecret, resumption_ms, log).

(* Prove equivalence between processLeft and processRight *)

channel start, c1, c2, c3.

let processLeft(hk: hashkey) =
    !N3 in(c1, ()); new ms: extracted; out(c1, ());
    ((!N in(c2, log0: bitstring); out(c2, Derive_Secret_cs_ats_exp(hk, ms, log0))) |
     (!N' in(c3, log0': bitstring); out(c3, Derive_Secret_rms(hk, ms, log0')))).

let processRight =
    !N3 in(c1, ()); out(c1, ());
    ((!N in(c2, log1: bitstring);
    	 find[unique] j <= N suchthat defined(log1[j], r[j]) && log1 = log1[j] then
	      out(c2, r[j])
	 else
	      new r: three_keys; out(c2, r)) |
     (!N' in(c3, log1': bitstring);
     	  find[unique] j' <= N' suchthat defined(log1'[j'], r'[j']) && log1' = log1'[j'] then
	      out(c3, r'[j'])
	 else
	      new r': key; out(c3, r'))).

equivalence
	in(start, ());
	new hk: hashkey; (* This key models the choice of the collision resistant hash function *)
	out(start, ()); (processLeft(hk) | Hash_oracle(hk))

	in(start, ());
	new hk: hashkey; (* This key models the choice of the collision resistant hash function *)
	out(start, ()); (processRight | Hash_oracle(hk))
	     
(* EXPECTED
All queries proved.
0.328s (user 0.324s + system 0.004s), max rss 23168K
END *)
