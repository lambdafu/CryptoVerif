(* Analysing the HPKE Standard - Supplementary Material
   Joël Alwen; Bruno Blanchet; Eduard Hauck; Eike Kiltz; Benjamin Lipp; 
   Doreen Riepel

This is supplementary material accompanying the paper:

Joël Alwen, Bruno Blanchet, Eduard Hauck, Eike Kiltz, Benjamin Lipp,
and Doreen Riepel. Analysing the HPKE Standard. In Anne Canteaut and
Francois-Xavier Standaert, editors, Eurocrypt 2021, Lecture Notes in
Computer Science, pages 87-116, Zagreb, Croatia, October 2021. Springer.
Long version: https://eprint.iacr.org/2020/1499 *)

proof {
  allowed_collisions default^4/large;
   (* We allow eliminating collisions with probability in power 4 of 
      N, Qeperuser, Qdperuser, Qcperuser times PCollKey, to allow q^2 * PCollKey,
      where q = N * (Qeperuser + Qdperuser + Qcperuser) *)
  (* on the left side *)
  crypto exclude_weak_keys(Z_t) **;
  out_game "l0.out.cv";
  (* Let appear this case distinction in the Decap oracle,
     that is present on the right side *)
  insert after "OADecap(pk_S"
    "find ic_1 = ic <= Qcperuser suchthat
      defined(zz_3[ic], sk'[ic], enc_2[ic])
      && (enc_2[ic] = cd)
      && (exp(g, sk'[ic]) = pk_S)
      then";
  simplify;
  out_game "l1.out.cv";
  out_game "l1occ.out.cv" occ;
  (* Use correctnes of the KEM: in the Decap oracle, in the case of
     honest participants, return the key chosen in the Encap oracle. *)
  replace
    at_nth 1 3 "return{[0-9]*}({[0-9]*}AuthDecap_Some({[0-9]*}zz_5))"
    "zz_3[ic_2]";
  remove_assign useless;
  simplify;
  out_game "l2.out.cv";
  out_game "l2occ.out.cv" occ;

  (* Let appear this case distinction in the Encap oracle.
     Treating messages between honest keys separately helps
     when applying GDH. *)
  insert after "OAEncap(pk_R"
    "find i1 <= N suchthat
      defined(x_2[i1])
      && pk_R = expnw(g, x_2[i1]) then";
  out_game "l3.out.cv";
  (* Use unique names for the assignments of the following variables
     that are currently not unique *)
  SArename x;
  SArename enc_3;
  remove_assign binder dh1_4;
  remove_assign binder dh2_4;
  SArename zz_4;
  SArename pkE_4;
  SArename kemContext_4;
  out_game "l4.out.cv";
  crypto prf_odh(prf) [variables: x_2 -> a, x_1 -> b .];
  all_simplify;
  out_game "l5.out.cv";
  crypto prf_convert(prf) **;
  crypto prf(prf2) **;
  all_simplify;  
  out_game "l.out.cv" occ;

  (* go to right side *)
  start_from_other_end;
  crypto exclude_weak_keys(Z_t) **;
  out_game "r0.out.cv";
  (* Let appear this case distinction in the Encap oracle.
     Treating messages between honest keys separately helps
     when applying GDH. *)
  insert after "OAEncap(pk_R_1"
    "find i2 <= N suchthat
      defined(x_11[i2])
      && pk_R_1 = expnw(g, x_11[i2]) then";
  out_game "r1.out.cv";
  (* Use unique names for the assignments of the following variables
     that are currently not unique *)
  SArename x_9;
  remove_assign binder dh1_10;
  remove_assign binder dh2_10;
  SArename enc_8;
  SArename pkE_10;
  out_game "r3.out.cv";
  crypto prf_odh(prf) [variables: x_11 -> a, x_10 -> b .];
  all_simplify;
  out_game "r4.out.cv";
  crypto prf_convert(prf) **;
  crypto prf(prf2) **;
  all_simplify;  
  remove_assign binder E_1;
  out_game "r5.out.cv";
  success
}

include(`common.dhkem.ocvl')

(* TO DO seems to be a non-standard collision-resistance/preimage resistance
   property *)
proba Coll2prf.
collision z <-R eae_output_t; forall x: G_t, y: bitstring;
	  return(z = prf(x,y)) <=(Coll2prf(time))=> return(false).


param N, Qeperuser, Qcperuser, Qdperuser.

table E(G_t, G_t, bitstring, eae_output_t).

equivalence
      foreach i <= N do Osetup() := sk <-R Z_t; return(); (
        foreach ic <= Qcperuser do (
	  Ochall(sk': Z_t) :=
            return(AuthEncap(pkgen(sk), skgen(sk')))) |
        foreach ie <= Qeperuser do (
	  OAEncap(pk_R: G_t) :=
            return(AuthEncap(pk_R, skgen(sk)))) |
        foreach id <= Qdperuser do (
	  OADecap(pk_S: G_t, cd: bitstring) :=
            return(AuthDecap(cd, skgen(sk), pk_S))) |
	(* The next oracle gives the public key to the adversary *)
        Opk() := return(pkgen(sk))
      )

      foreach i <= N do Osetup() := sk <-R Z_t; return(); (
        foreach ic <= Qcperuser do (
	  Ochall(sk': Z_t) :=
            let AuthEncap_tuple(k: eae_output_t, ce: bitstring) = AuthEncap(pkgen(sk), skgen(sk')) in (
              k' <-R eae_output_t;
	      insert E(pkgen(sk'), pkgen(sk), ce, k');
              return(AuthEncap_tuple(k', ce))
            ) else (
              (* Never happens because AuthEncap always returns AuthEncap_tuple(...) *)
              return(AuthEncap_None)
            )) |
        foreach ie <= Qeperuser do (
	  OAEncap(pk_R: G_t) :=
            return(AuthEncap(pk_R, skgen(sk)))) |
        foreach id <= Qdperuser do (
	  OADecap(pk_S: G_t, cd: bitstring) :=
	    get E(=pk_S, =pkgen(sk), =cd, k'') in (
              return(AuthDecap_Some(k''))
            ) else (
              return(AuthDecap(cd, skgen(sk), pk_S))
            )) |
        Opk() := return(pkgen(sk))
      )

(* EXPECTED FILENAME: examples/hpke/PRFODH/dhkem.auth.insider-cca-lr.m4.ocv TAG: 1
All queries proved.
0.768s (user 0.756s + system 0.012s), max rss 30028K
END *)
