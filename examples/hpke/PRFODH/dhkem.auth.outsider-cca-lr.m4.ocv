(* Analysing the HPKE Standard - Supplementary Material
   Joël Alwen; Bruno Blanchet; Eduard Hauck; Eike Kiltz; Benjamin Lipp; 
   Doreen Riepel

This is supplementary material accompanying the paper:

Joël Alwen, Bruno Blanchet, Eduard Hauck, Eike Kiltz, Benjamin Lipp,
and Doreen Riepel. Analysing the HPKE Standard. In Anne Canteaut and
Francois-Xavier Standaert, editors, Eurocrypt 2021, Lecture Notes in
Computer Science, Zagreb, Croatia, October 2021. Springer. To appear.
Long version: https://eprint.iacr.org/2020/1499 *)

proof {
  allowed_collisions default^4/large;
   (* We allow eliminating collisions with probability in power 4 of 
      N, Qeperuser, Qdperuser, Qcperuser times PCollKey, to allow q^2 * PCollKey,
      where q = N * (Qeperuser + Qdperuser + Qcperuser) *)
  (* start on the left side *)
  crypto exclude_weak_keys(Z_t) **;
  out_game "l1.out.cv";
  (* Let appear this case distinction in the Encap oracle,
     that is present on the right side. *)
  insert after "OAEncap(pk_R"
    "find i1 <= N suchthat
      defined(x_1[i1])
      && pk_R = expnw(g, x_1[i1]) then";
  (* Use unique names for the assignments of the following variables
     that are currently not unique *)
  SArename x;
  SArename enc_1;
  SArename zz_2;
  SArename pkE_2;
  SArename pkS;
  remove_assign binder dh1_2;
  remove_assign binder dh2_2;
  SArename kemContext_2;
  out_game "l2.out.cv";
  (* Let appear this case distinction in the Decap oracle,
     that is present on the right side *)
  insert after "OADecap(pk_S"
    "find ie1 <= Qeperuser, i1 <= N suchthat
      defined(x_1[i1], pk_R[ie1, i1], zz_10[ie1, i1], x_2[ie1, i1], enc_8[ie1, i1])
      && expnw(g, x_1) = pk_R[ie1, i1]
      && pk_S = expnw(g, x_1[i1])
      && enc_8[ie1, i1] = enc_2 then";
  out_game "l3.out.cv";
  out_game "l3occ.out.cv" occ;
  (* Use correctness of the KEM: In the Decap oracle for honest
     participants, return the key chosen in the Encap oracle.
     This replacement is done at the 1st line that matches the regex,
     at the 3rd term/occurrence number within the match (zz_3). *)
  replace
    at_nth 1 3 "return{[0-9]*}({[0-9]*}AuthDecap_Some({[0-9]*}zz_3))"
    "zz_10[ie1_2, i1_8]";
  all_simplify;
  remove_assign binder dh1_3;
  remove_assign binder dh2_3;
  out_game "l4.out.cv";
  (* Apply the PRF-ODH assumption, where the longterm static key sk (x_1), and
     the ephemeral key x_2 (generated in the Encaps oracle in the case
     of honest participants), are considered uncompromised. *)
  crypto prf_odh(prf) [variables: x_1 -> a, x_2 -> b .];
  all_simplify;
  out_game "l5.out.cv";
  crypto prf_convert(prf) **;
  crypto prf(prf2) **;
  all_simplify;  
  out_game "l6.out.cv";

  (* go to the right side *)
  start_from_other_end;
  crypto exclude_weak_keys(Z_t) **;
  out_game "r1.out.cv";
  remove_assign binder dh1_7;
  remove_assign binder dh2_7;
  remove_assign binder dh1_9;
  remove_assign binder dh2_9;
  out_game "r2.out.cv";
  crypto prf_odh(prf) [variables: x_9 -> a, x_8 -> b .];
  all_simplify;
  out_game "r3.out.cv";
  crypto prf_convert(prf) **;
  crypto prf(prf2) **;
  all_simplify;  
  out_game "r4.out.cv";
  success
}

include(`common.dhkem.ocvl')

param N, Qeperuser, Qdperuser.

equivalence
      foreach i <= N do Osetup() := sk <-R Z_t; return(); (
        foreach ie <= Qeperuser do (
	  OAEncap(pk_R: G_t) :=
            return(AuthEncap(pk_R, skgen(sk)))) |
        foreach id <= Qdperuser do (
	  OADecap(pk_S: G_t, enc: bitstring) :=
            return(AuthDecap(enc, skgen(sk), pk_S))) |
	(* The next oracle gives the public key to the adversary *)
        Opk() := return(pkgen(sk))
      )


      foreach i <= N do Osetup() := sk <-R Z_t; return(); (
        foreach ie <= Qeperuser do (
	  OAEncap(pk_R: G_t) :=
            find i1 <= N suchthat defined(sk[i1]) && pk_R = pkgen(sk[i1]) then (
              let AuthEncap_tuple(k: eae_output_t, ce: bitstring) = AuthEncap(pk_R, skgen(sk)) in (
                k' <-R eae_output_t;
                return(AuthEncap_tuple(k', ce))
	      )
	      else (
	        (* Never happens because AuthEncap always returns AuthEncap_tuple(...) *)
                return(AuthEncap_None)
              )
            ) else (
              return(AuthEncap(pk_R, skgen(sk)))
            )) |
        foreach id <= Qdperuser do (
	  OADecap(pk_S: G_t, cd: bitstring) :=
	    (* This "find" implements a lookup in the set E of the paper.
               The set is not built explicitly; we look for values in
               encryption queries using arrays -- all variables are implicitly
               stored in arrays indexed by replication indices above their
               definition. *)
            find ie1 <= Qeperuser, i1 <= N suchthat
              defined(sk[i1], pk_R[ie1, i1], k'[ie1, i1], ce[ie1, i1]) &&
                pkgen(sk) = pk_R[ie1, i1] && pkgen(sk[i1]) = pk_S && ce[ie1, i1] = cd then (
              return(AuthDecap_Some(k'[ie1, i1]))
            ) else (
              return(AuthDecap(cd, skgen(sk), pk_S))
            )) |
        Opk() := return(pkgen(sk))
      )

(* EXPECTED FILENAME: examples/hpke/PRFODH/dhkem.auth.outsider-cca-lr.m4.ocv TAG: 1
All queries proved.
0.496s (user 0.492s + system 0.004s), max rss 26796K
END *)
