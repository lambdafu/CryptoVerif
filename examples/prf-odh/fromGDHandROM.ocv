param qPRFDH, qPRFDHa, qPRFDHb.

type G [bounded,large].
type Z [bounded,large].

proba pGDH.
proba pDistRerandom.
expand DH_basic(G, Z, g, exp, exp', mult).
expand GDH(G, Z, g, exp, exp', mult, pGDH, pDistRerandom).

type hashkey [fixed].
type T.
type T' [bounded,large].
expand ROM_hash_large_2(hashkey, G, T, T', F, hashoracle, qF).

expand ROM_hash_large(hashkey, T, T', H, hashoracle', qH).

proof {
    auto;
    out_game "g1.cv" occ;
    replace 243 "false";
    replace 124 "false";
    replace 75 "false";
    replace 50 "false";
    simplify;    
    start_from_other_end;
    auto;
    remove_assign binder "x_O_[0-9]*";
    simplify;
    remove_assign useless;
    success }

equivalence
    Ogen() :=
    hk <-R hashkey;
    a <-R Z;
    b <-R Z;
    let A = exp(g,a) in
    let B = exp(g,b) in
    return();
    ((OA() := return(A)) |
     (OB() := return(B)) |
     (foreach i <= qPRFDH do OPRFDH(x : T) := return(F(hk, exp(g, mult(a,b)), x))) |
     (foreach i <= qPRFDHa do OPRFDHa(x : T, Y: G) := if exp(Y,a) = exp(g, mult(a,b)) then return(bottom) else return(F(hk, exp(Y,a), x))) |
     (foreach i <= qPRFDHb do OPRFDHb(x : T, X: G) := if exp(X,b) = exp(g, mult(a,b)) then return(bottom) else return(F(hk, exp(X,b), x))) |
     run hashoracle(hk))

    Ogen() :=
    hk <-R hashkey;
    hk' <-R hashkey;
    a <-R Z;
    b <-R Z;
    let A = exp'(g,a) in
    let B = exp'(g,b) in
    return();
    ((OA() := return(A)) |
     (OB() := return(B)) |
     (foreach i <= qPRFDH do OPRFDH(x : T) := return(H(hk', x))) |
     (foreach i <= qPRFDHa do OPRFDHa(x : T, Y: G) :=
        if exp'(Y,a) = exp'(g, mult(a,b)) then
	  return(bottom)
	else
	  return(F(hk, exp'(Y,a), x))) |
     (foreach i <= qPRFDHb do OPRFDHb(x : T, X: G) :=
        if exp'(X,b) = exp'(g, mult(a,b)) then
	  return(bottom)
	else
	  return(F(hk, exp'(X,b), x))) |
     run hashoracle(hk))

(* EXPECTED
All queries proved.
0.196s (user 0.188s + system 0.008s), max rss 21104K
END *)
