param qPRFDH, qPRFDHa, qPRFDHb.

type G [bounded,large].
type Z [bounded,large].

proba pGDH.
expand DH_basic(G, Z, g, exp, exp', mult).
expand GDH(G, Z, g, exp, exp', mult, pGDH).


equation forall x:G, x':G, y:Z; (exp(x,y) = exp(x',y)) = (x = x').
equation forall x:G, x':G, y:Z; (exp'(x,y) = exp'(x',y)) = (x = x').

equation forall x:G, x':Z, y:Z; (exp(x,y) = exp(g, mult(x',y))) = (x = exp(g,x')).
equation forall x:G, x':Z, y:Z; (exp'(x,y) = exp'(g, mult(x',y))) = (x = exp'(g,x')).

type hashkey [fixed].
type T.
type T' [bounded,large].
expand ROM_hash_2(hashkey, G, T, T', F, hashoracle, qF).

expand ROM_hash(hashkey, T, T', H, hashoracle', qH).

proof {
    auto;
    start_from_other_end;
    auto;
    remove_assign binder "x1_9";
    remove_assign binder "x1_10";
    simplify;
    remove_assign useless;
    success }

equivalence
    Ogen() :=
    hk <-R hashkey;
    a <-R Z;
    b <-R Z;
    let A = exp(g,a) in
    let B = exp(g,b) in
    return();
    ((OA() := return(A)) |
     (OB() := return(B)) |
     (foreach i <= qPRFDH do OPRFDH(x : T) := return(F(hk, exp(g, mult(a,b)), x))) |
     (foreach i <= qPRFDHa do OPRFDHa(x : T, Y: G) := if Y = B then return(bottom) else return(F(hk, exp(Y,a), x))) |
     (foreach i <= qPRFDHb do OPRFDHb(x : T, X: G) := if X = A then return(bottom) else return(F(hk, exp(X,b), x))) |
     run hashoracle(hk))

    Ogen() :=
    hk <-R hashkey;
    hk' <-R hashkey;
    a <-R Z;
    b <-R Z;
    let A = exp'(g,a) in
    let B = exp'(g,b) in
    return();
    ((OA() := return(A)) |
     (OB() := return(B)) |
     (foreach i <= qPRFDH do OPRFDH(x : T) := return(H(hk', x))) |
     (foreach i <= qPRFDHa do OPRFDHa(x : T, Y: G) :=
        if Y = B then
	  return(bottom)
	else
	  return(F(hk, exp'(Y,a), x))) |
     (foreach i <= qPRFDHb do OPRFDHb(x : T, X: G) :=
        if X = A then
	  return(bottom)
	else
	  return(F(hk, exp'(X,b), x))) |
     run hashoracle(hk))
